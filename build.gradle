
group = 'org.koshinuke'
version = '0.0.1'
description = 'static contents granerator'

apply from: file('gradle/convention.gradle')
apply from: file('gradle/check.gradle')

configurations.all { resolutionStrategy.cacheChangingModulesFor 14, 'days' }

repositories {
	mavenCentral()
	mavenRepo(url: 'http://download.eclipse.org/jgit/maven')
}

buildscript {
	repositories {
		mavenCentral()
	}
}

dependencies {
	groovy 'org.codehaus.groovy:groovy-all:1.8.+'

	compile 'org.thymeleaf:thymeleaf:2.0.+'
	// maven-ant-tasks-x.x.x.jarがnekohtmlを内包しており、
	// それがorg.apache.xerces.parsers.DOMParserを継承している為、回避措置。
	compile 'xerces:xercesImpl:2.10.0'

	compile 'org.eclipse.jgit:org.eclipse.jgit:2.+'
	compile 'com.google.guava:guava:12.+'
	compile 'org.pegdown:pegdown:1.1.+'

	compile 'org.lesscss:lesscss:1.3.+'

	compile 'org.mortbay.jetty:servlet-api:3.0.20100224'
	[
		'jetty-server',
		'jetty-servlet',
		'jetty-websocket'
	].each {
		compile ("org.eclipse.jetty:$it:8.+") { exclude group: 'org.eclipse.jetty.orbit' }
	}

	compile 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:1.+'
	compile 'commons-net:commons-net:3.+'
	compile 'rome:rome:1.0'

	compile gradleApi()

	testCompile 'junit:junit:4.+'
	testCompile ('org.apache.ftpserver:ftpserver:1.0.6') {
		exclude group: 'org.springframework'
		exclude group: 'log4j'
		exclude group: 'org.slf4j'
	}
	// testCompile 'org.spockframework:spock-core:0.6-groovy-1.8'
	// TODO: use geb
	// http://www.gebish.org/manual/current/build-integrations.html#gradle
}

apply from: file('gradle/ide.gradle')

task createWrapper(type: Wrapper) {
	gradleVersion = '1.0'
}

buildscript {
	repositories {
		flatDir { dir project.file('build/libs') }
		mavenCentral()
	}
	dependencies {
		classpath 'org.thymeleaf:thymeleaf:2.0.+'
		// maven-ant-tasks-x.x.x.jarがnekohtmlを内包しており、
		// それがorg.apache.xerces.parsers.DOMParserを継承している為、回避措置。
		classpath 'xerces:xercesImpl:2.10.0'
		classpath 'org.lesscss:lesscss:1.3.+'
		classpath 'com.google.guava:guava:12.+'
		classpath 'org.pegdown:pegdown:1.1.+'
		if(project.hasProperty("df")) {
			classpath 'org.koshinuke:yuzen:0.0.1'
		}
		classpath 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:1.+'
		classpath 'org.mortbay.jetty:servlet-api:3.0.20100224'
		[
			'jetty-server',
			'jetty-servlet',
			'jetty-websocket'
		].each {
			classpath ("org.eclipse.jetty:$it:8.+") { exclude group: 'org.eclipse.jetty.orbit' }
		}
		classpath 'org.eclipse.jgit:org.eclipse.jgit:2.+'
		classpath 'rome:rome:1.0'
	}
}
	
// -Pdf=true
if(project.hasProperty("df")) {
	apply plugin: 'yuzen'
	site {
		feed {
			syndicationURI = 'http://taichi.github.com/yuzen'
			author = 'taichi'
		}
	}
}
