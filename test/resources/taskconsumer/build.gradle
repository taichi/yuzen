import org.gradle.api.tasks.Copy

buildscript {
	repositories {
		flatDir { dirs '../../../build/libs' }
		mavenCentral()
	}
	dependencies {
		classpath 'org.thymeleaf:thymeleaf:2.0.+'
		// maven-ant-tasks-x.x.x.jarがnekohtmlを内包しており、
		// それがorg.apache.xerces.parsers.DOMParserを継承している為、回避措置。
		classpath 'xerces:xercesImpl:2.10.0'
		classpath 'org.lesscss:lesscss:1.3.+'
		classpath 'com.google.guava:guava:12.+'
		classpath 'org.pegdown:pegdown:1.1.+'
		classpath 'org.koshinuke:yuzen:0.0.1'
		classpath 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:1.+'
		classpath 'org.mortbay.jetty:servlet-api:3.0.20100224'
		[
			'jetty-server',
			'jetty-servlet',
			'jetty-websocket'
		].each {
			classpath ("org.eclipse.jetty:$it:8.+") { exclude group: 'org.eclipse.jetty.orbit' }
		}
		classpath 'org.eclipse.jgit:org.eclipse.jgit:2.+'
		classpath 'rome:rome:1.0'
	}
}

apply plugin: 'yuzen'

yuzen.publish {
	ghpages	repoURI : "https://github.com/taichi/testYuzen.git"
}

blog {
	title "ぶろぐ たいとる"
	subtitle "さぶさぶさぶたい"
	//head 'less'
	profile "![taichi](https://twimg0-a.akamaihd.net/profile_images/769940966/226710936a3042d473708005333d57118c925cac_normal.png) Trick Programmer"
	feed {
		syndicationURI = 'http://localhost:8080/blog'
		author = 'taichi'
	}
}

def dest = file("${project.buildDir}/html")

task less(type: org.koshinuke.yuzen.gradle.LessCompile) {
	source file("_templates/blog/less/main.less")
	destinationDir file("$dest/css")
	compress true
}

task copyJs(type: Copy) {
	from "_templates/blog/js"
	into "$dest/js"
}

task writeBlog(type: org.koshinuke.yuzen.gradle.DefaultContentsTask, dependsOn: [less, copyJs]) {
	destinationDir dest
	doFirst {
		blog.head 'css'
		blog.autoload true
	}
}

task designBlog(type: org.koshinuke.yuzen.gradle.DefaultContentsTask) {
	destinationDir dest
	doFirst {
		blog.head 'less'
		blog.autoload true
	}
}
