import org.gradle.api.tasks.Copy;
import org.koshinuke.yuzen.gradle.BlogTask;
import org.koshinuke.yuzen.gradle.LessCompile;

buildscript {
	repositories {
		flatDir { dirs '../../../build/libs' }
		mavenCentral()
	}
	dependencies {
		classpath 'org.thymeleaf:thymeleaf:2.0.+'
		// maven-ant-tasks-x.x.x.jarがnekohtmlを内包しており、
		// それがorg.apache.xerces.parsers.DOMParserを継承している為、回避措置。
		classpath 'xerces:xercesImpl:2.10.0'
		classpath 'org.lesscss:lesscss:1.3.+'
		classpath 'com.google.guava:guava:12.+'
		classpath 'org.pegdown:pegdown:1.1.+'
		classpath 'org.koshinuke:yuzen:0.0.1-SNAPSHOT'
		classpath 'org.mortbay.jetty:servlet-api:3.0.20100224'
		[
			'jetty-server',
			'jetty-servlet',
			'jetty-websocket'
		].each {
			classpath ("org.eclipse.jetty:$it:8.+") { exclude group: 'org.eclipse.jetty.orbit' }
		}
	}
}

apply plugin: 'yuzen'

blog {
	title "ぶろぐ たいとる"
	subtitle "さぶさぶさぶたい"
}

def dest = file("${project.buildDir}/html")

task less(type: LessCompile) {
	source file("_templates/blog/less/main.less")
	destinationDir file("$dest/css")
	compress true
}

task copyJs(type: Copy) {
	from "_templates/blog/js"
	into "$dest/js"
}

task writeBlog(type: BlogTask, dependsOn: [less, copyJs]) {
	destinationDir dest
	doFirst {
		blog.head 'css'
		blog.autoload true
	}
}

task designBlog(type: BlogTask) {
	destinationDir dest
	doFirst {
		blog.head 'less'
		blog.autoload true
	}
}
